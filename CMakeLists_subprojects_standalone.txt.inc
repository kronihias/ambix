GET_FILENAME_COMPONENT(SUBDIRNAME ${CMAKE_CURRENT_SOURCE_DIR} NAME)

IF( DEFINED SPECIFIC_PROJECTNAME )
	# this is for ambix_decoder (which shares the code of ambix_binaural)
	SET (SUBPROJECT_NAME ${SPECIFIC_PROJECTNAME}_o${AMBI_ORDER})
ELSE( DEFINED SPECIFIC_PROJECTNAME )
	# this is the normal way...
	SET (SUBPROJECT_NAME ${SUBDIRNAME}_o${AMBI_ORDER})
ENDIF(DEFINED SPECIFIC_PROJECTNAME )

# set the jack client name
ADD_DEFINITIONS(-DJUCE_JACK_CLIENT_NAME="${SUBPROJECT_NAME}")

# add the folder with Juce includes
INCLUDE_DIRECTORIES ( JuceLibraryCode )

ADD_DEFINITIONS(-DJUCE_APP_CONFIG_HEADER=<AppConfig.h>)

IF(DEFINED SPECIFIC_SOURE_DIR)
	INCLUDE_DIRECTORIES ( ${SPECIFIC_SOURE_DIR}/JuceLibraryCode )
ENDIF(DEFINED SPECIFIC_SOURE_DIR)

#############################
# add all c, cpp, cc files from the Source directory
FILE ( GLOB_RECURSE SOURCE Source/*.c* )

IF(DEFINED SPECIFIC_SOURE_DIR)
	FILE ( GLOB_RECURSE SOURCE ${SPECIFIC_SOURE_DIR}/Source/*.c* )
	# LIST ( APPEND SOURCE ${SOURCE_2})
ENDIF(DEFINED SPECIFIC_SOURE_DIR)

############################
# ignore some source files (specially made for ambix_decoder)
# little bit strange construction but it works...

IF(DEFINED IGNORE_SRC_FILES)

	FOREACH ( CUR_SRC ${SOURCE} )
		
		GET_FILENAME_COMPONENT(CUR_SRC_NAME ${CUR_SRC} NAME)
		
		LIST ( FIND IGNORE_SRC_FILES ${CUR_SRC_NAME} FOUND )
		IF(NOT ${FOUND} EQUAL -1)
			LIST ( FIND SOURCE ${CUR_SRC} REMOVE_ID )
			LIST ( REMOVE_AT SOURCE ${REMOVE_ID} )
			# MESSAGE ( STATUS "REMOVED SRC FILE: " ${CUR_SRC_NAME})
		ENDIF(NOT ${FOUND} EQUAL -1)
		
	ENDFOREACH ( CUR_SRC ${SOURCE} )
	
ENDIF(DEFINED IGNORE_SRC_FILES)


############################
# add from common directory
IF(WITH_SphericalHarmonic)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/SphericalHarmonic/ShChebyshev.cpp
		${SRC_DIR}/common/SphericalHarmonic/ShLegendre.cpp
		${SRC_DIR}/common/SphericalHarmonic/ShNorm.cpp
		${SRC_DIR}/common/SphericalHarmonic/SphericalHarmonic.cpp)
ENDIF(WITH_SphericalHarmonic)

IF(WITH_SphFilter)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/SphFilter/SphFilter.cpp)
ENDIF(WITH_SphFilter)

IF(WITH_MyMeter)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/MyMeter/MyMeter.cpp)
ENDIF(WITH_MyMeter)

IF(WITH_MyMeterDsp)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/MyMeterDsp/MyMeterDsp.cpp)
ENDIF(WITH_MyMeterDsp)

IF(WITH_T_DESIGN)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/Ressources/t_design.cpp)
ENDIF(WITH_T_DESIGN)

IF(WITH_LegendreU)
	LIST ( APPEND SOURCE ${SRC_DIR}/common/LegendreU/LegendreU.cpp)
ENDIF(WITH_LegendreU)

############################
# AUDIO UNIT SOURCE
IF (BUILD_AU)
	LIST ( APPEND SOURCE ${AU_PLUGIN_SOURCE})
ENDIF (BUILD_AU)

############################
# standalone filter source
FILE ( GLOB_RECURSE SOURCE_2 ${SRC_DIR}/JUCE/standalone-filter/*.c* )
LIST ( APPEND SOURCE ${SOURCE_2})

#SORT IT
LIST ( SORT SOURCE )

#######
# output dir
set (CMAKE_RUNTIME_OUTPUT_DIRECTORY ${BIN_DIR}/_bin/standalone)


############################
# add exec

IF (APPLE)
	ADD_EXECUTABLE(${SUBPROJECT_NAME} MACOSX_BUNDLE ${SOURCE} ${JUCE_STANDALONE_SOURCE})
ENDIF(APPLE)

IF(UNIX AND NOT APPLE AND NOT ANDROID)
    ADD_EXECUTABLE(${SUBPROJECT_NAME} ${SOURCE} ${JUCE_STANDALONE_SOURCE})
ENDIF(UNIX AND NOT APPLE AND NOT ANDROID)

IF (WIN32)
    ADD_EXECUTABLE(${SUBPROJECT_NAME} WIN32 ${SOURCE} ${JUCE_STANDALONE_SOURCE})
ENDIF (WIN32)

####################
# additional libs (FFTW3F, Eigen3 is header only)

IF(WITH_FFTW3)

	# MESSAGE( STATUS "LINKING FFTW3F: " ${FFTW3F_LIBRARY} )
	
	TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
	
		${FFTW3F_LIBRARY}
	)
ENDIF(WITH_FFTW3)


IF(WITH_LIBSOXR)
	TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
		${LIBSOXR_LIBRARIES}
	)
ENDIF(WITH_LIBSOXR)

####################
#plattform specifics

IF(NOT APPLE)

	#osx need special treating for creating the bundle...
	
	TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
		${JUCE_STATIC_LIB}
		${JUCE_LIBRARIES}
	)
	
	# get the target folder for later copying it
	GET_TARGET_PROPERTY(FILEPATH ${SUBPROJECT_NAME} LOCATION)
	
ENDIF(NOT APPLE)





IF(APPLE)

	#prototype Info.plist
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_INFO_PLIST ${SRC_DIR}/osx_ressources/MacOSXBundleInfo.plist.in)

	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES BUNDLE TRUE)
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES BUNDLE_EXTENSION vst)
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_BUNDLE_VERSION ${VERSION})
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_SHORT_VERSION_STRING ${VERSION})
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_LONG_VERSION_STRING ${VERSION})
	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES MACOSX_BUNDLE_BUNDLE_NAME ${SUBPROJECT_NAME})


	SET_TARGET_PROPERTIES(${SUBPROJECT_NAME} PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${BIN_DIR})

	# link libs
	TARGET_LINK_LIBRARIES( ${SUBPROJECT_NAME}
		${JUCE_STATIC_LIB}
		${JUCE_LIBRARIES}
	)

	IF( DEFINED OSX_COPY_LIB )
		#copy additional files (eg. dynamic libraries)
		ADD_CUSTOM_COMMAND(
			TARGET ${SUBPROJECT_NAME} POST_BUILD 
			COMMAND ${CMAKE_COMMAND} 
			ARGS -E copy ${SRC_DIR}/mac-libs/${OSX_COPY_LIB} ${BIN_DIR}/_bin/standalone/${SUBPROJECT_NAME}.app/Contents/Frameworks/${OSX_COPY_LIB}
			)
	ENDIF( DEFINED OSX_COPY_LIB )
	

ENDIF(APPLE)


################################################
# change to static linking for ms visual c++
IF(WIN32)

set(CompilerFlags
        CMAKE_CXX_FLAGS
        CMAKE_CXX_FLAGS_DEBUG
        CMAKE_CXX_FLAGS_RELEASE
        CMAKE_C_FLAGS
        CMAKE_C_FLAGS_DEBUG
        CMAKE_C_FLAGS_RELEASE
        )
foreach(CompilerFlag ${CompilerFlags})
  string(REPLACE "/MD" "/MT" ${CompilerFlag} "${${CompilerFlag}}")
endforeach()

ENDIF(WIN32)
